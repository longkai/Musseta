buildscript {
  repositories {
    maven { url rootProject.ext.repositories.fabric }
  }
  dependencies {
    classpath rootProject.ext.dependencies.android
    classpath rootProject.ext.dependencies.retrolambda
    classpath rootProject.ext.dependencies.apt
    classpath rootProject.ext.dependencies.fabric
    classpath rootProject.ext.dependencies.googleServices
  }
}
apply plugin: rootProject.ext.plugins.android
apply plugin: rootProject.ext.plugins.retrolambda
apply plugin: rootProject.ext.plugins.apt
apply plugin: rootProject.ext.plugins.fabric
apply plugin: rootProject.ext.plugins.googleServices

repositories {
  maven { url rootProject.ext.repositories.fabric }
}

// NOTE: you need provide you own signing configs in `gradle.properties`
def enableSigning = file("${rootDir}/gradle.properties").exists()
// NOTE: if you want to enable fabric facilities, please provide your own `fabric.properties` file, plz refer fabric doc
def enableFabric = file("${projectDir}/fabric.properties").exists()
def isCi = "true".equals(System.getenv("CI"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

android {
  compileSdkVersion rootProject.ext.versions.compileSdk
  buildToolsVersion rootProject.ext.versions.buildTools

  dexOptions {
    // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
    preDexLibraries = preDexEnabled && !isCi
  }
//  publishNonDefault true

  defaultConfig {
    applicationId "yuejia.liu.musseta"
    minSdkVersion 9
    targetSdkVersion rootProject.ext.versions.targetSdk
    versionCode 1
    versionName "0.2.0"

    manifestPlaceholders = ["channel": "dev"] // default to dev chanel

    buildConfigField "String", "PRODUCT_HUNT_API_KEY", "\"${PRODUCT_HUNT_API_KEY}\""
    buildConfigField "String", "PRODUCT_HUNT_API_SETRECT", "\"${PRODUCT_HUNT_API_SETRECT}\""

    buildConfigField "String", "DRIBBBLE_CLIENT_ACCESS_TOKEN", "\"${DRIBBBLE_CLIENT_ACCESS_TOKEN}\""

    testInstrumentationRunner 'yuejia.liu.musseta.MussetaTestingRunner'

    testInstrumentationRunnerArguments disableAnalytics: 'true'
//    testInstrumentationRunnerArgument "argument1", "make_test_fail"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  signingConfigs {
    release {
      try {
        storeFile file(STORE_FILE)
        storePassword STORE_PASSWORD
        keyAlias KEY_ALIAS
        keyPassword KEY_PASSWORD
      } catch (ex) {
        if (enableSigning) {
          throw new InvalidUserDataException("You should define STORE_FILE, KEYSTORE_PASSWORD, KEY_ALIAS and KEY_PASSWORD in gradle.properties.")
        }
      }
    }
  }

  buildTypes {
    debug {
      applicationIdSuffix ".debug"
//      testCoverageEnabled = true // todo: seems upgrade to AS 2.0 preview, it sucks, make it work when is fixed.
//      minifyEnabled true
      ext.enableCrashlytics = false // disable fabric in any debug build
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      testProguardFile 'proguard-test-rules.pro'
    }
    release {
      if (enableSigning) {
        signingConfig signingConfigs.release
      }

      minifyEnabled true
      ext.enableCrashlytics = enableFabric
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  // Always show the result of every unit test, even if it passes.
  testOptions.unitTests.all {
    testLogging {
      events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
    }
  }

  lintOptions {
    abortOnError false
    disable 'InvalidPackage'
  }

  packagingOptions {
    exclude 'META-INF/services/javax.annotation.processing.Processor'
    exclude 'LICENSE.txt'
  }

  productFlavors {
    alpha {
      applicationId android.defaultConfig.applicationId + '.alpha'
    }
    beta {
      applicationId android.defaultConfig.applicationId + '.beta'
    }
    musseta {
      applicationId android.defaultConfig.applicationId
      manifestPlaceholders = ["channel": "musseta"]
    }
  }
}

configurations {
  androidTestCompile.exclude group: 'com.android.support', module: 'support-v4'
  androidTestCompile.exclude group: 'com.android.support', module: 'recyclerview-v7'
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])

  compile rootProject.ext.dependencies.palette

  compile rootProject.ext.dependencies.rxandroid
  compile rootProject.ext.dependencies.rxjava

  compile rootProject.ext.dependencies.appcompat
  compile rootProject.ext.dependencies.design
  compile rootProject.ext.dependencies.recyclerview
  compile rootProject.ext.dependencies.preference
  compile rootProject.ext.dependencies.gridlayout
  compile rootProject.ext.dependencies.cardview

  compile rootProject.ext.dependencies.butterKnife
  compile rootProject.ext.dependencies.timber
  compile rootProject.ext.dependencies.picasso
  compile rootProject.ext.dependencies.retrofit
  compile rootProject.ext.dependencies.okhttp
  compile rootProject.ext.dependencies.dagger
  apt rootProject.ext.dependencies.daggerCompiler
  provided rootProject.ext.dependencies.javax

  compile(rootProject.ext.dependencies.crashlytics) {
    transitive = true;
  }
  compile rootProject.ext.dependencies.analytics

  // testing
  androidTestCompile rootProject.ext.dependencies.mockito
  androidTestCompile rootProject.ext.dependencies.dexmaker
  androidTestCompile rootProject.ext.dependencies.dexmakerMockito

  androidTestCompile rootProject.ext.dependencies.rules
  androidTestCompile rootProject.ext.dependencies.runner
  androidTestCompile rootProject.ext.dependencies.espresso
  androidTestCompile rootProject.ext.dependencies.espressoContrib
  androidTestCompile rootProject.ext.dependencies.espressoIntents

  androidTestApt rootProject.ext.dependencies.daggerCompiler
}
